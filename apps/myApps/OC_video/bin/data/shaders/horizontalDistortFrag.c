// fragment shader
#version 410

//
// Description : Array and textureless GLSL 2D simplex noise function.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : ijm
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
// 

vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec2 mod289(vec2 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 permute(vec3 x) {
  return mod289(((x*34.0)+1.0)*x);
}

float snoise(vec2 v)
  {
  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
// First corner
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);

// Other corners
  vec2 i1;
  //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
  //i1.y = 1.0 - i1.x;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  // x0 = x0 - 0.0 + 0.0 * C.xx ;
  // x1 = x0 - i1 + 1.0 * C.xx ;
  // x2 = x0 - 1.0 + 2.0 * C.xx ;
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;

// Permutations
  i = mod289(i); // Avoid truncation effects in permutation
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
        + i.x + vec3(0.0, i1.x, 1.0 ));

  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;

// Gradients: 41 points uniformly over a line, mapped onto a diamond.
// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;

// Normalise gradients implicitly by scaling m
// Approximation of: m *= inversesqrt( a0*a0 + h*h );
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

// Compute final noise value at P
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}

//end noise code

uniform sampler2DRect video;
uniform sampler2DRect scanlines;
uniform float time;
uniform float amplitude;
uniform float wavelength;
uniform float speed;
uniform float opacity;
uniform float scrollSpeed;
uniform float height;

in vec2 texcoordVarying;
out vec4 outputColor;

//the origin for the gl_FragCoord by default starts in the lower left
//this sets it to the upper left
// layout(origin_upper_left) in vec4 gl_FragCoord;

void main(){
    //extract just the x and y values from the current fragmentCoordinates
    vec2 pos = gl_FragCoord.xy * vec2(1280.0/640.0, 720.0/480.0);

    //want the unmodified texture coordiniate
    vec2 originalPos = pos;
    
    //shift the x-position with a sin wave
    pos.x += amplitude * cos((pos.y + time * speed)/ wavelength); 

    //shift with noise
    pos.x += amplitude * snoise(vec2(pos.y + time * speed, 0)/wavelength);

    //height of frame
    pos.y = mod(pos.y + scrollSpeed * time, height);

    vec4 scanlineColor = texture(scanlines, originalPos);

    //get sample color from texture
    vec4 sampleColor = texture(video, pos);

    //remove a proportional amount of color based on the scanline texture color
    sampleColor.rgb = sampleColor.rgb - sampleColor.rgb * scanlineColor.a;

    //using mix performs the same function as the commented line below
    outputColor = sampleColor;
    // outputColor = vec4(texCoordVarying.x/1280.0, texCoordVarying.y/720.0, 0, 1.0);
}